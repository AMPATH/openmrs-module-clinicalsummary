<?xml version="1.0" encoding="UTF-8"?>
<xsl:stylesheet version="2.0"
	xmlns:xsl="http://www.w3.org/1999/XSL/Transform" xmlns:fo="http://www.w3.org/1999/XSL/Format"
	xmlns:fn="http://www.w3.org/2005/02/xpath-functions"
	exclude-result-prefixes="fo">
	<xsl:output method="xml" version="1.0" omit-xml-declaration="no" indent="yes" />

	<xsl:template match="clinicalSummary">
		<fo:root xmlns:fo="http://www.w3.org/1999/XSL/Format">
			<fo:layout-master-set>
				<fo:simple-page-master master-name="simple" page-height="297mm" page-width="210mm" margin-top="1cm" margin-left="1cm" margin-right="1cm" margin-bottom="1cm">
					<fo:region-body margin-top="1.2cm" margin-bottom="1.2cm" />
					<fo:region-before extent="3cm" />
					<fo:region-after extent="1cm" />
				</fo:simple-page-master>
			</fo:layout-master-set>

			<!-- Begin Summary -->
			<fo:page-sequence master-reference="simple">
				<!-- Header -->
				<fo:static-content flow-name="xsl-region-before" font-family="Helvetica">
					<fo:block text-align="center" border-top="thin solid grey" border-left="thin solid grey" border-right="thin solid black" border-bottom="thin solid black" background-color="#e0e0e0" padding="0.2cm" space-after="8pt" font-weight="bold">
						AMPATH Medical Record System Clinical Summary
					</fo:block>
				</fo:static-content>
				<!-- Footer -->
				<fo:static-content flow-name="xsl-region-after" font-family="Helvetica">
					<fo:table table-layout="fixed" width="100%">
						<fo:table-column column-width="60%" />
						<fo:table-column column-width="40%" />
						<fo:table-body>
							<fo:table-row height="1cm">
								<fo:table-cell display-align="center">
									<fo:block text-align="left" font-size="8pt">
										<xsl:if test="lastEncounter/location">
											<fo:block>
												Last seen
												<xsl:value-of select="lastEncounter/dateTime" />
												at
												<xsl:value-of select="lastEncounter/location" />
												by
												<xsl:value-of select="lastEncounter/provider" />
											</fo:block>
											<fo:block>
												Encounter entered by
												<xsl:value-of select="lastEncounter/creator" />
												on
												<xsl:value-of select="lastEncounter/createdDate" />
											</fo:block>
										</xsl:if>
										<fo:block>
											Next scheduled visit: 
											<xsl:variable name="returnVisit" select="returnVisit" />
											<xsl:choose>
												<xsl:when test="$returnVisit=''">
													Unknown
												</xsl:when>
												<xsl:otherwise>
													<xsl:value-of select="returnVisit" />
												</xsl:otherwise>
											</xsl:choose>
										</fo:block>
									</fo:block>
								</fo:table-cell>
								<fo:table-cell display-align="center">
									<fo:block text-align="right" font-size="20pt" font-weight="bold">
										<xsl:value-of select="id" />
									</fo:block>
								</fo:table-cell>
							</fo:table-row>
						</fo:table-body>
					</fo:table>
					<fo:block>
					</fo:block>
				</fo:static-content>

				<!-- Begin Page Content -->
				<fo:flow flow-name="xsl-region-body" font-family="Helvetica" font-size="10pt">
					<fo:table table-layout="fixed" width="100%">
						<fo:table-column column-width="25%" />
						<fo:table-column column-width="25%" />
						<fo:table-column column-width="10%" />
						<fo:table-column column-width="20%" />
						<fo:table-column column-width="20%" />
						<fo:table-body>
							<fo:table-row>
								<fo:table-cell number-columns-spanned="2">
									<fo:block font-weight="bold" font-style="italic" font-size="20pt">
										<xsl:value-of select="name" />
									</fo:block>
								</fo:table-cell>
								<fo:table-cell text-align="center">
									<fo:block font-weight="bold" font-style="italic" font-size="8pt">
										<xsl:variable name="sequence" select="sequences/source" />
										<xsl:variable name="total" select="count(reminders/reminder)" />
										<xsl:for-each select="reminders/reminder">
											<xsl:variable name="source" select="@source" />
											<xsl:value-of select="$sequence[@name=$source]" />
											<xsl:if test="position() &lt; $total">
												<xsl:text>,</xsl:text>
											</xsl:if>
										</xsl:for-each>
									</fo:block>
								</fo:table-cell>
								<fo:table-cell number-columns-spanned="2" text-align="right">
									<fo:block font-weight="bold" font-size="20pt">
										<xsl:value-of select="id" />
									</fo:block>
								</fo:table-cell>
							</fo:table-row>
							<fo:table-row>
								<fo:table-cell number-columns-spanned="3">
									<fo:block font-size="10pt" space-after="6pt">
										<xsl:choose>
											<xsl:when test="gender = 'M'">
												<fo:inline> Male </fo:inline>
											</xsl:when>
											<xsl:when test="gender = 'F'">
												<fo:inline> Female </fo:inline>
											</xsl:when>
										</xsl:choose>
										<fo:inline>
											<xsl:value-of select="birthdate/age" />
											(
											<xsl:variable name="birthdateEstimated" select="birthdate/estimated" />
											<xsl:if test="$birthdateEstimated='true'">
												~
											</xsl:if>
											<xsl:value-of select="birthdate/value" />
											)
										</fo:inline>
									</fo:block>
								</fo:table-cell>
								<fo:table-cell number-columns-spanned="2" number-rows-spanned="2" text-align="right">
									<fo:block font-size="10pt" space-after="6pt">
										<xsl:if test="alternateId">
											<xsl:variable name="maxIds" select="2" />
											<xsl:for-each select="alternateId">
												<xsl:choose>
													<xsl:when test="position() &lt; $maxIds">
														<fo:block color="#888888">
															<fo:inline>
																<xsl:value-of select="." />
															</fo:inline>
														</fo:block>
													</xsl:when>
													<xsl:when test="position() = ($maxIds + 1)">
														<fo:block color="#888888">
															<fo:inline font-style="italic">(not all ids shown)
															</fo:inline>
														</fo:block>
													</xsl:when>
												</xsl:choose>
											</xsl:for-each>
										</xsl:if>
									</fo:block>
								</fo:table-cell>
							</fo:table-row>
							<fo:table-row>
								<fo:table-cell number-columns-spanned="3">
									<fo:block>
										<fo:inline>
											<xsl:attribute name="font-weight">bold</xsl:attribute>
											HIV STATUS:
										</fo:inline>
										<fo:inline>
											<xsl:value-of select="hivTest/result" />
											<xsl:if test="hivTest/date">
												(<xsl:value-of select="hivTest/date" />)
											</xsl:if>
										</fo:inline>
									</fo:block>
								</fo:table-cell>
							</fo:table-row>
						</fo:table-body>
					</fo:table>
					<fo:table table-layout="fixed" width="100%" space-before="6pt">
						<fo:table-column column-width="25%" />
						<fo:table-column column-width="25%" />
						<fo:table-column column-width="25%" />
						<fo:table-column column-width="25%" />
						<fo:table-body font-size="10pt">
							<fo:table-row>
								<fo:table-cell text-align="center" border-right="thin solid black" border-bottom="thin solid black" padding="2pt">
									<fo:block>First Encounter</fo:block>
								</fo:table-cell>
								<fo:table-cell text-align="center" border-right="thin solid black" border-bottom="thin solid black" padding="2pt">
									<fo:block>Highest WHO Stage</fo:block>
								</fo:table-cell>
								<fo:table-cell number-columns-spanned="2" text-align="center" border-bottom="thin solid black" padding="2pt">
									<fo:block>6 Months HIV Rx Adherence</fo:block>
								</fo:table-cell>
							</fo:table-row>
							<fo:table-row>
								<fo:table-cell text-align="center" border-right="thin solid black" padding="2pt">
									<fo:block>
										<xsl:if test='firstEncounterDate and string-length(normalize-space(firstEncounterDate)) > 0'>
											<fo:inline>
												<xsl:value-of select="firstEncounterDate" />
											</fo:inline>
										</xsl:if>
									</fo:block>
								</fo:table-cell>
								<fo:table-cell text-align="center" border-right="thin solid black" padding="2pt">
									<fo:block>
										<xsl:if test="whoStage">
											<fo:inline>
												<xsl:value-of select="whoStage" />
											</fo:inline>
										</xsl:if>
									</fo:block>
								</fo:table-cell>
								<fo:table-cell number-columns-spanned="2" text-align="center" padding="2pt">
									<fo:block>
										<xsl:if test="whoStage">
											<xsl:value-of select="perfectAdherence" />
										</xsl:if>
									</fo:block>
								</fo:table-cell>
							</fo:table-row>
						</fo:table-body>
					</fo:table>

					<!-- Problem/Med List table -->
					<xsl:variable name="maxRows" select="10" />
					<fo:table table-layout="fixed" width="100%" space-before="6pt">
						<fo:table-column column-width="35%" />
						<fo:table-column column-width="30%" />
						<fo:table-column column-width="35%" />
						<fo:table-body font-size="20pt">
							<fo:table-row>
							
								<fo:table-cell>
									<!-- Problem listi -->
									<fo:block font-size="12pt" space-before="6pt" font-weight="bold">
										<fo:inline text-decoration="underline">Problem List</fo:inline>
									</fo:block>
									<fo:block font-size="6pt" font-style="italic" space-after="3pt">
										<fo:inline>Remove resolved problems through encounter form
										</fo:inline>
									</fo:block>
									<xsl:choose>
										<xsl:when test="problemList/problems">
											<xsl:element name="fo:list-block">
												<xsl:attribute name="font-size">10pt</xsl:attribute>
												<xsl:attribute name="space-after">12pt</xsl:attribute>
												<xsl:attribute name="provisional-label-separation">1mm</xsl:attribute>
												<xsl:attribute name="provisional-distance-between-starts">1mm</xsl:attribute>
												<xsl:for-each select="problemList/problems">
													<xsl:variable name="i" select="position()" />
													<xsl:if test="$i &lt;= $maxRows">
														<fo:list-item>
															<fo:list-item-label end-indent="label-end()" text-align="right">
																<fo:block>
																	<xsl:value-of select="position()" />.
																</fo:block>
															</fo:list-item-label>
															<fo:list-item-body start-indent="body-start()">
																<fo:block>
																	<xsl:variable name="totalProblem" select="count(problem)" />
																	<xsl:for-each select="problem">
																		<xsl:if test="position() = 1">
																			<xsl:value-of select="value" />
																			<xsl:if test="datetime">
																				<xsl:element name="fo:inline">
																					<xsl:attribute name="font-size">8pt</xsl:attribute>
																					<xsl:attribute name="font-style">italic</xsl:attribute>
																					<xsl:attribute name="color">#555</xsl:attribute>
																					<xsl:text> (</xsl:text>
																					<xsl:value-of select="datetime" />
																					<xsl:if test="$totalProblem > 1">
																						<xsl:text> ...</xsl:text>
																						<xsl:value-of select="$totalProblem - 1" />
																						<xsl:text> more</xsl:text>
																					</xsl:if>
																					<xsl:text>)</xsl:text>
																				</xsl:element>
																			</xsl:if>
																		</xsl:if>
																	</xsl:for-each>
																</fo:block>
															</fo:list-item-body>
														</fo:list-item>
													</xsl:if>
												</xsl:for-each>
												<xsl:if test="count(problemList/problems) > $maxRows">
													<fo:list-item>
														<fo:list-item-label end-indent="label-end()">
															<fo:block></fo:block>
														</fo:list-item-label>
														<fo:list-item-body start-indent="body-start()">
															<fo:block font-style="italic">
																<xsl:text>(</xsl:text>
																<xsl:value-of select="count(problemList/problems) - $maxRows" />
																<xsl:text> more problems)</xsl:text>
															</fo:block>
														</fo:list-item-body>
													</fo:list-item>
												</xsl:if>
											</xsl:element>
										</xsl:when>
										<xsl:otherwise>
											<fo:block font-size="10pt">NONE</fo:block>
										</xsl:otherwise>
									</xsl:choose>
								</fo:table-cell>
								
								<fo:table-cell>
									<!-- Immunizations -->
									<fo:block font-size="12pt" space-before="6pt" space-after="6pt" font-weight="bold">
										<fo:inline text-decoration="underline">Immunizations</fo:inline>
									</fo:block>
									<xsl:choose>
										<xsl:when test="immunizations/immunization">
											<xsl:element name="fo:list-block">
												<xsl:attribute name="font-size">10pt</xsl:attribute>
												<xsl:attribute name="space-after">12pt</xsl:attribute>
												<xsl:attribute name="provisional-label-separation">1mm</xsl:attribute>
												<xsl:attribute name="provisional-distance-between-starts">1mm</xsl:attribute>
												<xsl:for-each select="immunizations/immunization">
													<xsl:variable name="i" select="position()" />
													<xsl:if test="$i &lt;= $maxRows">
														<fo:list-item>
															<fo:list-item-label end-indent="label-end()"
																text-align="right">
																<fo:block>
																	<xsl:value-of select="position()" />.
																</fo:block>
															</fo:list-item-label>
															<fo:list-item-body start-indent="body-start()">
																<fo:block>
																	<xsl:value-of select="drug" />
																	<xsl:choose>
																		<xsl:when test="dose != ''">
																			<xsl:text> (</xsl:text>
																			<xsl:value-of select="dose" />
																			<xsl:text>)</xsl:text>
																		</xsl:when>
																		<xsl:otherwise>
																			<xsl:text> [Unknown Dose]</xsl:text>
																		</xsl:otherwise>
																	</xsl:choose>
																</fo:block>
															</fo:list-item-body>
														</fo:list-item>
													</xsl:if>
												</xsl:for-each>
												<xsl:if test="count(immunizations/immunization) > $maxRows">
													<fo:list-item>
														<fo:list-item-label end-indent="label-end()">
															<fo:block></fo:block>
														</fo:list-item-label>
														<fo:list-item-body start-indent="body-start()">
															<fo:block font-style="italic">
																<xsl:text>(</xsl:text>
																<xsl:value-of select="count(immunizations/immunization) - $maxRows" />
																<xsl:text> more meds)</xsl:text>
															</fo:block>
														</fo:list-item-body>
													</fo:list-item>
												</xsl:if>
											</xsl:element>
										</xsl:when>
										<xsl:otherwise>
											<fo:block font-size="10pt">NONE</fo:block>
										</xsl:otherwise>
									</xsl:choose>
								</fo:table-cell>
								
								<fo:table-cell>
									<!-- Medications -->
									<fo:block font-size="12pt" space-before="6pt" space-after="6pt" font-weight="bold">
										<fo:inline text-decoration="underline">Recent ARVs &amp; OI Meds</fo:inline>
									</fo:block>
									<xsl:choose>
										<xsl:when test="medications/medication">
											<xsl:element name="fo:list-block">
												<xsl:attribute name="font-size">10pt</xsl:attribute>
												<xsl:attribute name="space-after">12pt</xsl:attribute>
												<xsl:attribute name="provisional-label-separation">1mm</xsl:attribute>
												<xsl:attribute name="provisional-distance-between-starts">1mm</xsl:attribute>
												<xsl:for-each select="medications/medication">
													<xsl:variable name="i" select="position()" />
													<xsl:if test="$i &lt;= $maxRows">
														<fo:list-item>
															<fo:list-item-label end-indent="label-end()"
																text-align="right">
																<fo:block>
																	<xsl:value-of select="position()" />.
																</fo:block>
															</fo:list-item-label>
															<fo:list-item-body start-indent="body-start()">
																<fo:block>
																	<xsl:value-of select="value" />
																</fo:block>
															</fo:list-item-body>
														</fo:list-item>
													</xsl:if>
												</xsl:for-each>
												<xsl:if test="count(medications/medication) > $maxRows">
													<fo:list-item>
														<fo:list-item-label end-indent="label-end()">
															<fo:block></fo:block>
														</fo:list-item-label>
														<fo:list-item-body start-indent="body-start()">
															<fo:block font-style="italic">
																<xsl:text>(</xsl:text>
																<xsl:value-of select="count(medications/medication) - $maxRows" />
																<xsl:text> more meds)</xsl:text>
															</fo:block>
														</fo:list-item-body>
													</fo:list-item>
												</xsl:if>
											</xsl:element>
										</xsl:when>
										<xsl:otherwise>
											<fo:block font-size="10pt">NONE</fo:block>
										</xsl:otherwise>
									</xsl:choose>
									<!-- Medications -->
									<fo:block font-size="12pt" space-before="6pt" space-after="6pt" font-weight="bold">
										<fo:inline text-decoration="underline">ARV Side Effects</fo:inline>
									</fo:block>
									<xsl:choose>
										<xsl:when test="arvSideEffects/arvSideEffect">
											<xsl:element name="fo:list-block">
												<xsl:attribute name="font-size">10pt</xsl:attribute>
												<xsl:attribute name="space-after">12pt</xsl:attribute>
												<xsl:attribute name="provisional-label-separation">1mm</xsl:attribute>
												<xsl:attribute name="provisional-distance-between-starts">1mm</xsl:attribute>
												<xsl:for-each select="arvSideEffects/arvSideEffect">
													<xsl:variable name="i" select="position()" />
													<xsl:if test="$i &lt;= $maxRows">
														<fo:list-item>
															<fo:list-item-label end-indent="label-end()"
																text-align="right">
																<fo:block>
																	<xsl:value-of select="position()" />.
																</fo:block>
															</fo:list-item-label>
															<fo:list-item-body start-indent="body-start()">
																<fo:block>
																	<xsl:value-of select="." />
																</fo:block>
															</fo:list-item-body>
														</fo:list-item>
													</xsl:if>
												</xsl:for-each>
												<xsl:if test="count(arvSideEffects/arvSideEffect) > $maxRows">
													<fo:list-item>
														<fo:list-item-label end-indent="label-end()">
															<fo:block></fo:block>
														</fo:list-item-label>
														<fo:list-item-body start-indent="body-start()">
															<fo:block font-style="italic">
																<xsl:text>(</xsl:text>
																<xsl:value-of select="count(arvSideEffects/arvSideEffect) - $maxRows" />
																<xsl:text> more meds)</xsl:text>
															</fo:block>
														</fo:list-item-body>
													</fo:list-item>
												</xsl:if>
											</xsl:element>
										</xsl:when>
										<xsl:otherwise>
											<fo:block font-size="10pt">NONE</fo:block>
										</xsl:otherwise>
									</xsl:choose>
								</fo:table-cell>
								
							</fo:table-row>
						</fo:table-body>
					</fo:table>
					
					<!-- PMTCT -->
					<fo:block font-size="12pt" space-before="12pt" font-weight="bold">
						<fo:inline text-decoration="underline">Maternal pMTCT: Med / Period / Doses Given / Rx Length</fo:inline>
					</fo:block>
					<xsl:choose>
						<xsl:when test="pmtcts/pmtct">
							<fo:list-block font-size="10pt" space-before="6pt">
								<xsl:for-each select="pmtcts/pmtct">
									<fo:list-item>
										<fo:list-item-label end-indent="label-end()" text-align="right">
											<fo:block>
												<xsl:value-of select="position()" />
												<xsl:text>.</xsl:text>
											</fo:block>
										</fo:list-item-label>
										<fo:list-item-body start-indent="body-start()">
											<fo:block space-before="6pt">
												<xsl:value-of select="drug" />
												<xsl:text> / </xsl:text>
												<xsl:value-of select="dose" />
												<xsl:text> / </xsl:text>
												<xsl:choose>
													<xsl:when test="doseQuantification != ''">
														<xsl:value-of select="doseQuantification" />
													</xsl:when>
													<xsl:otherwise>[Unknown Dose]</xsl:otherwise>
												</xsl:choose>
												<xsl:text> / </xsl:text>
												<xsl:choose>
													<xsl:when test="treatmentWeek != ''">
														<xsl:value-of select="treatmentWeek" />
														<xsl:text> Weeks</xsl:text>
													</xsl:when>
													<xsl:otherwise>Unknown</xsl:otherwise>
												</xsl:choose>
											</fo:block>
										</fo:list-item-body>
									</fo:list-item>
								</xsl:for-each>
							</fo:list-block>
						</xsl:when>
						<xsl:otherwise>
							<fo:block>
								<xsl:attribute name="font-size">10pt</xsl:attribute>
								NONE
							</fo:block>
						</xsl:otherwise>
					</xsl:choose>
					
					<!-- Flowsheet -->
            		<fo:block font-size="14pt" font-weight="bold" space-before="12pt">
						<fo:inline text-decoration="underline">Flowsheet (Initial + Last Four Value)</fo:inline>
					</fo:block>
					<fo:table space-after="12pt" width="100%" border-collapse="collapse">
						<xsl:for-each select="flowsheet/results">
							<fo:table-column />
						</xsl:for-each>
						<fo:table-body>
							<fo:table-row>
								<xsl:for-each select="flowsheet/results">
									<xsl:element name="fo:table-cell">
										<xsl:attribute name="text-align">center</xsl:attribute>
										<xsl:attribute name="padding">2pt</xsl:attribute>
										<xsl:attribute name="border-bottom">thin solid black</xsl:attribute>
										<xsl:if test="not(position()=last())">
											<xsl:attribute name="border-right">thin solid black</xsl:attribute>
										</xsl:if>
										<fo:block font-size="10" font-weight="bold">
											<xsl:value-of select="@name" />
										</fo:block>
									</xsl:element>
								</xsl:for-each>
							</fo:table-row>
							<xsl:variable name="results" select="flowsheet/results" />
							<xsl:variable name="cd4_percent" select="cd4_percent" />
							<xsl:for-each select="(1,2,3,4,5)">
								<xsl:variable name="i" select="position()" />
								<xsl:element name="fo:table-row">
									<xsl:attribute name="background-color">
                                		<xsl:if test="$i mod 2">#F8F8F8</xsl:if>
                                		<xsl:if test="not($i mod 2)">#FFFFFF</xsl:if>
                            		</xsl:attribute>
									<xsl:attribute name="height">1cm</xsl:attribute>
									<xsl:for-each select="$results">
										<xsl:variable name="result_name" select="@name" />
										<xsl:element name="fo:table-cell">
											<xsl:attribute name="text-align">center</xsl:attribute>
											<xsl:attribute name="display-align">center</xsl:attribute>
											<xsl:attribute name="padding">2pt</xsl:attribute>
											<xsl:if test="not(position()=last())">
												<xsl:attribute name="border-right">thin solid black</xsl:attribute>
											</xsl:if>
											<xsl:choose>
												<xsl:when test="$i = 1">
													<xsl:variable name="element" select="element[$i]" />
													<xsl:variable name="result_date" select="$element/datetime" />
													<fo:block font-size="10" space-after="0pt">
														<xsl:value-of select="$element/value" />
														<xsl:if test="$result_name='CD4'">
															<xsl:variable name="percentage" select="$cd4_percent/element[datetime=$result_date]" />
															<xsl:if test="$percentage">
																<fo:inline font-size="6pt" font-style="italic" space-before="0pt">
																	<xsl:text>(</xsl:text>
																	<xsl:value-of select="$percentage/value" />
																<xsl:text>%)</xsl:text>
																</fo:inline>
															</xsl:if>
														</xsl:if>
													</fo:block>
													<xsl:if test="$element/status != ''">
														<fo:block font-size="6pt" font-style="italic" space-before="0pt">
														<xsl:text>(</xsl:text>
															<xsl:value-of select="$element/status" />
															<xsl:text>)</xsl:text>
														</fo:block>
													</xsl:if>
													<xsl:if test="$element/datetime">
														<fo:block font-size="6pt" font-style="italic" color="#808080" space-before="0pt">
															<xsl:value-of select="$element/datetime" />
														</fo:block>
													</xsl:if>
												</xsl:when>
												<xsl:otherwise>
		                                            <xsl:variable name="j" select="max((count(element)-5,0)) + $i" />
													<xsl:variable name="element" select="element[$j]" />
													<xsl:variable name="result_date" select="$element/datetime" />
													<fo:block font-size="10" space-after="0pt">
														<xsl:value-of select="$element/value" />
														<xsl:if test="$result_name='CD4'">
															<xsl:variable name="percentage" select="$cd4_percent/element[datetime=$result_date]" />
															<xsl:if test="$percentage">
																<fo:inline font-size="6pt" font-style="italic" space-before="0pt">
																	<xsl:text>(</xsl:text>
																	<xsl:value-of select="$percentage/value" />
																<xsl:text>%)</xsl:text>
																</fo:inline>
															</xsl:if>
														</xsl:if>
													</fo:block>
													<xsl:if test="$element/status != ''">
														<fo:block font-size="6pt" font-style="italic" space-before="0pt">
														<xsl:text>(</xsl:text>
															<xsl:value-of select="$element/status" />
															<xsl:text>)</xsl:text>
														</fo:block>
													</xsl:if>
													<xsl:if test="$element/datetime">
														<fo:block font-size="6pt" font-style="italic" color="#808080" space-before="0pt">
															<xsl:value-of select="$element/datetime" />
														</fo:block>
													</xsl:if>
												</xsl:otherwise>
											</xsl:choose>
										</xsl:element>
									</xsl:for-each>
								</xsl:element>
							</xsl:for-each>
						</fo:table-body>
					</fo:table>
					<!-- Chest X-ray -->
					<fo:block font-size="12pt" space-before="12pt" font-weight="bold">
						<fo:inline text-decoration="underline">Last 2 Chest X-Rays</fo:inline>
						<fo:inline font-size="8pt" font-style="italic">
							(check chart as needed for results prior to 14-Feb-2006)
                		</fo:inline>
					</fo:block>
					<xsl:choose>
						<xsl:when test="cxrs/cxr">
							<fo:list-block font-size="12pt" space-before="6pt">
								<xsl:for-each select="cxrs/cxr">
									<fo:list-item>
										<fo:list-item-label end-indent="label-end()" text-align="right">
											<fo:block>
												<xsl:value-of select="position()" />.
											</fo:block>
										</fo:list-item-label>
										<fo:list-item-body start-indent="body-start()">
											<fo:block space-before="6pt">
												<xsl:value-of select="date" />
												<xsl:text>:</xsl:text>
												<xsl:value-of select="findings" />
											</fo:block>
										</fo:list-item-body>
									</fo:list-item>
								</xsl:for-each>
							</fo:list-block>
						</xsl:when>
						<xsl:otherwise>
							<fo:block>
								<xsl:attribute name="font-size">12pt</xsl:attribute>
								No chest x-ray results available.
							</fo:block>
						</xsl:otherwise>
					</xsl:choose>
					<!-- Reminders -->
					<fo:block space-before="12pt" width="100%">
						<xsl:attribute name="border-bottom">thin solid black</xsl:attribute>
					</fo:block>
					<fo:block font-size="12pt" space-before="6pt">
						<fo:inline font-size="14pt" text-decoration="underline" font-weight="bold">Reminders</fo:inline>
						: (Write number next to each reminder)
					</fo:block>
					<fo:block font-size="8pt" font-style="italic">
						<fo:inline>1-Ordered Today, 2-Not Applicable, 3-Previously
							Ordered, 4-Pt Allergic, 5-Pt Refused, 6-I Disagree with Reminder,
							7-Other(Explain)</fo:inline>
					</fo:block>
					<xsl:if test="reminders/reminder">
						<fo:list-block font-size="12pt" space-before="6pt">
							<xsl:for-each select="reminders/reminder">
								<fo:list-item>
									<fo:list-item-label end-indent="label-end()" text-align="right">
										<fo:block>
											<xsl:value-of select="position()" />.
										</fo:block>
									</fo:list-item-label>
									<fo:list-item-body start-indent="body-start()">
										<fo:block space-before="6pt">
											<xsl:value-of select="." />
											(___)
										</fo:block>
									</fo:list-item-body>
								</fo:list-item>
							</xsl:for-each>
						</fo:list-block>
					</xsl:if>
				</fo:flow>
			</fo:page-sequence>
		</fo:root>
	</xsl:template>

</xsl:stylesheet>